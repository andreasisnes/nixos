{
  lib,
  config,
  options,
  ...
}:
let
  symlinks = import ./symlinks.nix { inherit lib; };
in
{
  options = {
    host = lib.mkOption {
      type = lib.types.attrs;
      description = "Host environment variables accessible in all modules";
    };
    home = lib.mkOption {
      type = lib.types.deferredModule;
      default = {};
      description = "home-manager.users.<username> alias";
    };
  };

  config = lib.mkMerge [
    {
      _module.args = {
        host = config.host;
        inherit (symlinks) mkSymlinks;
      };
    }
    (lib.optionalAttrs (options ? home-manager) {
      home-manager.users.${config.host.username} = config.home;
      home-manager.extraSpecialArgs = {
        host = config.host;
        inherit (symlinks) mkSymlinks;
      };
    })
  ];
}